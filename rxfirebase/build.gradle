plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'


// This is the library version used when deploying the artifact
version = "0.2.0-SNAPSHOT"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.5.4'

    compile 'io.reactivex.rxjava2:rxjava:2.0.2'
    compile 'com.google.firebase:firebase-auth:10.0.1'
}


def siteUrl = 'https://github.com/cantrowitz/RxFirebase'      // Homepage URL of the library
def gitUrl = 'git@github.com:cantrowitz/RxFirebase.git'   // Git repository URL
group = "com.cantrowitz"                                          // Maven Group ID for the artifact


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}


artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = this.hasProperty('BINTRAY_USER') ? BINTRAY_USER : "Not Supported"
    key = this.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : "Not Supported"
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'com.cantrowitz:rxfirbase'
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        publish = false
    }
}

